PROGRAM -> program ident semi BLOCK dot
   
BLOCK -> BLOCK_I COMPOUND_STATEMENT
BLOCK_I-> CONSTANT_DEFINITION BLOCK_I
BLOCK_I-> VARIABLE_DECLARATION_PART BLOCK_I
BLOCK_I-> PROCEDURE_AND_FUNCTION_DECLARATION_PART BLOCK_I
BLOCK_I-> 
   

CONSTANT_DEFINITION -> const ident equal CONSTANT semi CONSTANT_DEFINITION_I
CONSTANT_DEFINITION_I-> ident equal CONSTANT semi
CONSTANT_DEFINITION_I-> 

CONSTANT -> UNSIGNED_NUMBER
CONSTANT -> SIGN UNSIGNED_NUMBER
CONSTANT -> ident
CONSTANT -> SIGN ident
CONSTANT -> string_literal
   
UNSIGNED_NUMBER -> num_int
UNSIGNED_NUMBER -> num_real
   
SIGN -> plus
SIGN -> minus
   

VARIABLE_DECLARATION_PART -> var VARIABLE_DECLARATION VARIABLE_DECLARATION_PART_I semi
VARIABLE_DECLARATION_PART_I-> semi VARIABLE_DECLARATION VARIABLE_DECLARATION_PART_I
VARIABLE_DECLARATION_PART_I-> 
   
VARIABLE_DECLARATION -> IDENTIFIER_LIST colon TYPE_IDENTIFIER
   
IDENTIFIER_LIST -> ident IDENTIFIER_LIST_I
IDENTIFIER_LIST_I-> comma ident
IDENTIFIER_LIST_I-> 

TYPE_IDENTIFIER -> ident
TYPE_IDENTIFIER -> integer
TYPE_IDENTIFIER -> real
TYPE_IDENTIFIER -> string
   

PROCEDURE_AND_FUNCTION_DECLARATION_PART -> PROCEDURE_OR_FUNCTION_DECLARATION semi

PROCEDURE_OR_FUNCTION_DECLARATION -> PROCEDURE_DECLARATION
PROCEDURE_OR_FUNCTION_DECLARATION -> FUNCTION_DECLARATION
   
PROCEDURE_DECLARATION -> procedure ident FORMAL_PARAMETER_LIST_OPT semi BLOCK
FORMAL_PARAMETER_LIST_OPT -> FORMAL_PARAMETER_LIST
FORMAL_PARAMETER_LIST_OPT -> 

FORMAL_PARAMETER_LIST -> lparen PARAMETER_GROUP FORMAL_PARAMETER_LIST_I rparen   
FORMAL_PARAMETER_LIST_I-> semi PARAMETER_GROUP FORMAL_PARAMETER_LIST_I
FORMAL_PARAMETER_LIST_I-> 

PARAMETER_GROUP -> IDENTIFIER_LIST colon TYPE_IDENTIFIER

FUNCTION_DECLARATION -> function ident FORMAL_PARAMETER_LIST_OPT colon TYPE_IDENTIFIER semi BLOCK


STATEMENT -> SIMPLE_STATEMENT
STATEMENT -> STRUCTURED_STATEMENT

SIMPLE_STATEMENT -> ASSIGNMENT_STATEMENT
SIMPLE_STATEMENT -> PROCEDURE_STATEMENT
SIMPLE_STATEMENT -> EMPTY_STATEMENT

ASSIGNMENT_STATEMENT -> ident assign EXPRESSION

PROCEDURE_STATEMENT -> ident PROCEDURE_STATEMENT_I
PROCEDURE_STATEMENT_I-> lparen PARAMETER_LIST rparen
PROCEDURE_STATEMENT_I-> 

STRUCTURED_STATEMENT -> COMPOUND_STATEMENT
STRUCTURED_STATEMENT -> IF_STATEMENT
STRUCTURED_STATEMENT -> REPETETIVE_STATEMENT

COMPOUND_STATEMENT -> begin  STATEMENT COMPOUND_STATEMENT_I end
COMPOUND_STATEMENT_I-> semi STATEMENT COMPOUND_STATEMENT_I
COMPOUND_STATEMENT_I->

IF_STATEMENT -> if EXPRESSION then STATEMENT IF_STATEMENT_I
IF_STATEMENT_I-> else STATEMENT
IF_STATEMENT_I->

REPETETIVE_STATEMENT -> WHILE_STATEMENT
REPETETIVE_STATEMENT -> FOR_STATEMENT

WHILE_STATEMENT -> while EXPRESSION do STATEMENT

FOR_STATEMENT -> for ident assign EXPRESSION to EXPRESSION do STATEMENT
   
EMPTY_STATEMENT ->


EXPRESSION -> SIMPLE_EXPRESSION EXPRESSION_I
EXPRESSION_I-> RELATIONAL_OPERATOR EXPRESSION
EXPRESSION_I->

RELATIONAL_OPERATOR -> equal
RELATIONAL_OPERATOR -> not_equal
RELATIONAL_OPERATOR -> lt
RELATIONAL_OPERATOR -> le
RELATIONAL_OPERATOR -> ge
RELATIONAL_OPERATOR -> gt

SIMPLE_EXPRESSION -> TERM SIMPLE_EXPRESSION_I
SIMPLE_EXPRESSION_I-> ADDITIVE_OPERATOR SIMPLE_EXPRESSION
SIMPLE_EXPRESSION_I->

ADDITIVE_OPERATOR -> plus
ADDITIVE_OPERATOR -> minus
ADDITIVE_OPERATOR -> or

TERM -> SIGNED_FACTOR TERM_I
TERM_I-> MULTIPLICATIVE_OPERATOR TERM
TERM_I->

MULTIPLICATIVE_OPERATOR -> star
MULTIPLICATIVE_OPERATOR -> div
MULTIPLICATIVE_OPERATOR -> mod
MULTIPLICATIVE_OPERATOR -> and

SIGNED_FACTOR -> SIGNED_FACTOR_I FACTOR
SIGNED_FACTOR_I-> plus
SIGNED_FACTOR_I-> minus
SIGNED_FACTOR_I->

FACTOR -> ident
FACTOR -> lparen EXPRESSION rparen
FACTOR -> UNSIGNED_CONSTANT
FACTOR -> not FACTOR 

UNSIGNED_CONSTANT -> UNSIGNED_NUMBER
UNSIGNED_CONSTANT -> string_literal

PARAMETER_LIST -> EXPRESSION PARAMETER_LIST_I
PARAMETER_LIST_I-> comma EXPRESSION PARAMETER_LIST_I
PARAMETER_LIST_I-> 
   